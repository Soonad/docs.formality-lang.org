# Core Features

## Let

Formality includes `let` expressions, which allow you to give names to expression.

```javascript
main : Output
  let hello = "Hello, world!"
  print(hello)
```

`let` expressions can be infinitely nested.

```javascript
main : Output
  let output = 
    let hello = "Hello, world!"
    print(hello)
  output
```

Note that `let` simply performs a basic parse-time substitution.

## Words

Formality includes native, unsigned, 32-bit numbers, and many numeric operations:

name | syntax | JavaScript equivalent
--- | --- | ---
addition | `x + y` | `(x + y) >>> 0`
subtraction | `x - y` | `(x - y) >>> 0`
multiplication | `x * y` | `(x * y) >>> 0`
division | `x / y` | `(x / y) >>> 0`
modulus | `x % y` | `(x % y) >>> 0`
exponentiation | `x ^ y` | `(x ** y) >>> 0`
bitwise-and | `x .& y` | `x & y`
bitwise-or | `x .| y` | `x | y`
bitwise-xor | `x .^ y` | `x ^ y`
bitwise-not | `.!(y)` | `~y`
bitwise-right-shift | `x .>> y` | `x >>> y`
bitwise-left-shift | `x .<< y` | `x << y`
greater-than | `x .> y` | `x > y ? 1 : 0`
less-than | `x .< y` | `x < y ? 1 : 0`
equals | `x .= y` | `x === y ? 1 : 0`
float-addition | `x +f y` | `x + y`
float-subtraction | `x -f y` | `x - y`
float-multiplication | `x *f y` | `x * y`
float-division | `x /f y` | `x / y`
float-modulus | `x %f y` | `x % y`
float-exponentiation | `x ^f y` | `x ** y`
uint-to-float | `.f(y)` | - 
float-to-uint | `.u(y)` | -

The type of a native number is `Word`. A number literal can be written in decimal:

```javascript
main : Word
  1900
```

Or in hexadecimal:

```javascript
main : Word
  0x76C
```

Or in binary:

```javascript
main : Word
  0b11101101100
```

Operators work as expected, except there is no precedence: parenthesis are always placed on the right. An expression like `3 * 10 + 1`, for example, is always parsed as `3 * (10 + 1)`. If you want the multiplication to occur first, you must be explicit:

```javascript
main : Word
  (3 * 10) + 1
```

## If / Else

`if` allows branching with a `Word` condition.

syntax | description
--- | ---
`if n: a else: b` | If `n .= 0`, evaluates to `a`, else, evaluates to `b`

Usage is straightforward:

```javascript
main : Output
  let age = 30

  if age .< 18:
    print("boring teenager")
  else:
    print("respect your elders!")
```

## Pairs

Formality includes native pairs.

syntax | description
--- | ---
`[x : A, B(x)]` | The type of a pair
`[a, b]` | Creates a pair with elements `a` and `b`
`fst(p)` | Extracts the first element of a pair
`snd(p)` | Extracts the second element of a pair
`get [a, b] = p ...` | Extracts both elements of a pair

Note that the type of a pair is `[x : A, B(x)]`, because the type of the second element can depend on the value of the first. When it doesn't, you can write just `[:A, B]` instead. Using pairs is straightforward.

- Creating, extracting one element with `fst`

```javascript
main : Word
  let pair = [1, 2]
  fst(pair)
```

- Creating, extracting both elements with `get`

```javascript
main : Word
  let pair  = [1, 2]
  get [a,b] = pair
  a + b
```

- Nesting

```javascript
main : [:Word, Word]
  let pairs = [[1, 2], [3, 4]]
  fst(pairs)
```
- Heterogenic

```javascript
main : [:[:Word,Word], String]
  [[1, 2], "Hello Word!"]
```

## Functions

Formality includes primitives for creating and applying functions:

syntax | description
--- | ---
`{x : A, y : B, z : C, ...} -> D` | Function type with args `x : A`, `y : B`, `z : C`, returning `D`
`{x, y, z, ...} body` | A function that receives the arguments `x`, `y`, `z` and returns `body` 
`f(x, y, z, ...)` | Applies the function `f` to the arguments `x`, `y`, `z` (curried)

Formality functions are anonymous expressions, like Haskell's lambdas. It has no multi-argument lambdas; `{x, y, z, ...} body` is just a syntax-sugar for `{x} {y} {z} ... body`. This is the same as JavaScript's `x => y => z => ... body`, or Haskell's `\ x y z ... -> body`. Function calls use the more conventional `f(x, y, z)` syntax, which, since functions are curried, is just a syntax-sugar for `f(x)(y)(z)...`.

The type of a function is `{x : A, y : B, z : C ...} -> D`, which is equivalent to Agda's `(x : A) -> (y : B) -> (z : C) -> ... D`. As usual, function types can be shortened as `A -> B` when `x` is unused. When annotating a top-level definition with a functional type, you don't need to write the lambdas, they are added implicity. For example:

```javascript
birth_to_age : {birth : Word} -> Word
  2019 - birth

main : Word
  birth_to_age(1990)
```

Lambdas and applications can be erased with a `~`, which causes them to vanish from the compiled output. This is useful, for example, to write polymorphic functions without extra runtime costs. For example, this is the polymorphic identity function:

```javascript
id : {~T : Type, x : T} -> T
  x

main : Word
  id(~Word, 42)
```

Formality functions are affine, which means you can't use a variable more than once. For example, the program below isn't allowed:

```javascript
copy : {b : Bool} -> [:Bool, Bool]
  [b, b]
```

Because `b` is used twice. While this sounds limiting, there are many ways to deal with it, as will be explained later, and it is extremelly important for both logical consistency and runtime performance.

## Boxes and Copying

Since functions are affine, Formality includes primives for performing explicit, deep copies of terms, as long as they're "boxed".

syntax | description
--- | ---
`#t` | Puts term `t` inside a box
`!T` | The type of a boxed term
`dup x = t; u` | Unboxes `t` and copies it as `x` inside `u`

Since this increases the power of the language considerably, in order not to cause logical inconsistencies, and to still allow for an efficient runtime, boxes are limited by the "stratification condition". It enforces that the number of `#`s surrounding a term must never change during reduction. As such, boxes aren't very useful for copying data, but are essential to implement control structures like loops and recursion. This all will be explained in more details later on.

## Equality

Formality includes untyped equality primitives, the same one seen in [Cedille](https://github.com/cedille/cedille).

syntax | description
--- | ---
`a == b` | A type asserting that `a` is equal to `b`
`refl(~x)` | A proof that `x == x`
`sym(~e)` | Given `e : a == b`, proves `b == a`
`t :: rewrite x in P(x) with e` | Given `e : a == b` and `t : P(a)`, proves `P(b)`

Those primitives are useful for theorem proving, as will be explained later.

## Self Types

Formality also has [Self Types](http://homepage.divms.uiowa.edu/~astump/papers/fu-stump-rta-tlca-14.pdf), which allow it us to implement inductive datatypes with Î»-encodings:

syntax | description
--- | ---
`$self T(self)` | `T` is a type that can access its own value
`new(~T) t` | Constructs an instance of a `T` with value `t`
`(%t)` | Consumes a self-type `t`, giving its type access to its value

This primitive is explained in more details on the paper linked above. While Self Types are extremelly useful and powerful, they should be considered an advanced feature that most users won't need use directly. Formality includes a large set of syntax-sugars that allow users to define and use datatypes with a familiar notation, similar to other traditional functional languages.

## Holes

Formality also features holes, which are very useful for development and debugging. A hole can be used to fill a part of your program that you don't want to implement yet. It can be written anywhere as `?name`, with the name being optional. This will cause Formality to print the type expected on the hole location, as well as the its context (scope variables). For example, the program below:

```javascript
import Base@0

main : {x : Bool} -> Bool
  and(true, ?)
```

Will output:

```javascript
[ERROR]
Hole found.
- With goal... Bool
- Inside of... and(true, ?help)
- With context:
- x : Bool
```

This tells you that, on the location of the hole, you should have a `Bool`.

The point of holes is that Formality will assume them to be true, allowing you to move on to other parts of your program before returning. Note that this is only automatic if Formality can infer the expected type of the hole's location. Otherwise, you must give it an explicit annotation, as in `: MyType ?hole`.

## Logs

Another handy feature is `log(x)`. When running a program, it will print the normal form of `x`, similarly to JavaScript's `console.log` and Python's `print`, but for anything (not only strings). When type-checking a program, it tells you the normal-form and the type of `x`. This is useful when you want to know what type an expression would have inside certain context. For example:

```javascript
import Base@0

main : {f : Bool -> Nat} -> Nat
  log(f(true))
  ?
```

Type-checking the program above will cause Formality to output:

```
[LOG]
Term: f(true)
Type: Nat

[ERROR]
Hole found.
- With goal... Nat
- Inside of... {f} => ?
- With context:
- f : {:Bool} -> Nat
```

This tells you that, inside the body of `main`, the type of `f(true)` is `Nat`. Since it coincides with the type of the hole, you can complete the program above with it:

```javascript
import Base@0

main : {f : Bool -> Nat} -> Nat
  f(true)
```

## Local imports

The `import` statement can be used to include local files. For example, save an `Answers.fm` file in the same directory as `hello.fm`, with the following contents:

```javascript
import Base@0

everything : String
  "42"
```

Then save a `test.fm` file as:

```javascript
import Base@0
import Answers

main : Output
  print(everything)
```

And run it with `fm test/main`. You should see `42`.

If multiple imports have conflicting names, you can disambiguate with `File/name`, or with a qualified import, using `as`:


```javascript
import Base@0
import Answers as A

main : Output
  print(A/everything)
```

## Global imports

Formality also has a file-based package manager. You can use it to share files with other people. A file can be saved globally with `fm -s file`. This will give it a unique name with a version, such as `file@7`. Once given a unique name, the file contents will never change, so `file@7` will always refer to that exact file. As soon as it is saved globally, you can import it from any other computer. For example, remove `Answers.fm` and change `hello.fm` to:

```javascript
import Base@0
import Answers@0

main : Output
  print(everything)
```

This will load `Answers@0.fm` inside the `fm_modules` directory and load it. Any import ending with `@N` refers to a unique, immutable, permanent global file. That prevents the infamous "depencency hell", and is useful for many applications.

Right now, global imports are uploaded to our servers, but, in a future, they'll upload files to a decentralized storage such as IPFS/Swarm, and give it a unique name using Ethereum's naming system.
